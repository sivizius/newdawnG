Macro yasic@@optimisePrefixNegative
  local temp
  temp                                  yasic@@pop
  If ( temp.type = yasic@@tConstant )
    yasic@@push                         yasic@@tConstant,             ( 0 - temp.value )
  Else
    yasic@@push                         temp.type,                    temp.value,         temp.size,          temp.misc
    yasic@@push                         yasic@@tOperator,             yasic@@opPrefixPositive
  End If
End Macro
Macro yasic@@optimiseAdd
  Local temp0, temp1
  temp1                                 yasic@@pop
  temp0                                 yasic@@pop
  If      (( temp0.type = yasic@@tConstant ) & \
           ( temp1.type = yasic@@tConstant ))
    yasic@@push                         yasic@@tConstant,             ( temp0.value + temp1.value )
  Else If (( temp0.type = yasic@@tConstant ) & \
           ( temp0.value = null ))
    yasic@@push                         temp1.type,                   temp1.value,        temp1.size,         temp1.misc
  Else If (( temp1.type = yasic@@tConstant ) & \
           ( temp1.value = null ))
    yasic@@push                         temp0.type,                   temp0.value,        temp0.size,         temp0.misc
  Else
    yasic@@push                         temp0.type,                   temp0.value,        temp0.size,         temp0.misc
    yasic@@push                         temp1.type,                   temp1.value,        temp1.size,         temp1.misc
    yasic@@push                         yasic@@tOperator,             yasic@@opAdd
  End If
End Macro
Macro yasic@@optimiseSub
  Local temp0, temp1
  temp1                                 yasic@@pop
  temp0                                 yasic@@pop
  If (( temp0.type = yasic@@tConstant ) & \
      ( temp1.type = yasic@@tConstant ))
    yasic@@push                         yasic@@tConstant,             ( temp0.value - temp1.value )
  Else If (( temp0.type = yasic@@tConstant ) & \
           ( temp0.value = null ))
    yasic@@push                         temp1.type,                   temp1.value,        temp1.size,         temp1.misc
    yasic@@push                         yasic@@tOperator,             yasic@@opPrefixNegative
  Else If (( temp1.type = yasic@@tConstant ) & \
           ( temp1.value = null ))
    yasic@@push                         temp0.type,                   temp0.value,        temp0.size,         temp0.misc
  Else
    yasic@@push                         temp0.type,                   temp0.value,        temp0.size,         temp0.misc
    yasic@@push                         temp1.type,                   temp1.value,        temp1.size,         temp1.misc
    yasic@@push                         yasic@@tOperator,             yasic@@opSub
  End If
End Macro
Macro yasic@@optimiseMul
  Local temp0, temp1, sign
  temp1                                 yasic@@pop
  temp0                                 yasic@@pop
  If      (( temp0.type = yasic@@tConstant ) & \
           ( temp1.type = yasic@@tConstant ))
    sign                                =                                       1
    If ( temp0.value and ( 1 shl 63 ))
      temp0                             =                                       ( 0 - temp0.value )
      sign                              =                                       ( 0 - sign )
    Else
      temp0                             =                                       ( temp0.value )
    End If
    If ( temp1.value and ( 1 shl 63 ))
      temp1                             =                                       ( 0 - temp1.value )
      sign                              =                                       ( 0 - sign )
    Else
      temp1                             =                                       ( temp1.value )
    End If
    yasic@@push                         yasic@@tConstant,             (( sign * temp0 * temp1 ) and nil )
  Else If ((( temp0.type = yasic@@tConstant ) & \
            ( temp0.value = null ))           | \
           (( temp1.type = yasic@@tConstant ) & \
            ( temp1.value = null )))
    If ( temp0.type = yasic@@tFunction )
      While ( yasic@@ptrStack >= 16 )
        temp0                             yasic@@pop
        If ( temp0.type = yasic@@tItem )
          Break
        End If
      End While
    End If
    yasic@@push                         yasic@@tConstant,             null
  Else If (( temp0.type = yasic@@tConstant ) & \
           ( temp0.value = 1 ))
    yasic@@push                         temp1.type,                   temp1.value,        temp1.size,         temp1.misc
  Else If (( temp1.type = yasic@@tConstant ) & \
           ( temp1.value = 1 ))
    yasic@@push                         temp0.type,                   temp0.value,        temp0.size,         temp0.misc
  Else
    yasic@@push                         temp0.type,                   temp0.value,        temp0.size,         temp0.misc
    yasic@@push                         temp1.type,                   temp1.value,        temp1.size,         temp1.misc
    yasic@@push                         yasic@@tOperator,             yasic@@opMul
  End If
End Macro
Macro yasic@@optimiseConnectStrings
  Local temp0, temp1
  temp1                                 yasic@@pop
  temp0                                 yasic@@pop
  If (( temp0.type = yasic@@tConstString ) & \
      ( temp1.type = yasic@@tConstString ))
    temp1                               yasic@@connectStrings         temp0,              temp1
    yasic@@push                         yasic@@tConstString,          temp1.value,        temp1.size
  Else
    yasic@@push                         temp0.type,                   temp0.value,        temp0.size,         temp0.misc
    yasic@@push                         temp1.type,                   temp1.value,        temp1.size,         temp1.misc
    yasic@@push                         yasic@@tOperator,             yasic@@opConnectStrings
  End If
End Macro
Macro yasic@@optimiseExpression
  Local temp
  yasic@@ptrQueque                      =                                       null
  yasic@@ptrStack                       =                                       null
  While ( yasic@@ptrQueque < yasic@@endQueque )
    temp                                yasic@@deque
    If ( temp.type = yasic@@tOperator )
      If 0
      Else If ( temp.value = yasic@@opPrefixPositive )
        ;do nothing, +a is always equal to a, isn't it?
      Else If ( temp.value = yasic@@opPrefixNegative )
        yasic@@optimisePrefixNegative
      Else If ( temp.value = yasic@@opAdd )
        yasic@@optimiseAdd
      Else If ( temp.value = yasic@@opSub )
        yasic@@optimiseSub
      Else If ( temp.value = yasic@@opMul )
        yasic@@optimiseMul
      Else If ( temp.value = yasic@@opConnectStrings )
        yasic@@optimiseConnectStrings
      Else
        yasic@@push                     temp.type,          temp.value,         temp.size,          temp.misc
      End If
    Else If ( temp.type = yasic@@tFunction )
      If      ( temp.misc = 0 )
        yasic@@push                     yasic@@tFunction,   temp.value,         temp.size
      ;Else If ( temp.misc = yasic@@funcTypeOf )
      Else
        Err 10, '[asm:yasic] unknown constant function'
      End If
    Else
      yasic@@push                       temp.type,          temp.value,         temp.size,          temp.misc
    End If
  End While
  yasic@@ptrQueque                      =                                       null
  yasic@@endQueque                      =                                       null
  yasic@@topStack                       =                                       null
  yasic@@unstackToQueque
End Macro
Macro yasic@@killOptimiser
  Purge yasic@@optimisePrefixNegative, yasic@@optimiseAdd, yasic@@optimiseSub, yasic@@optimiseMul
  Purge yasic@@optimiseExpression, yasic@@optimiseConnectStrings
End Macro