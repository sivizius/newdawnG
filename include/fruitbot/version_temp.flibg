Match +,-
Macro code                              language
  fbc0@@offString                       =                                       ( 0 )
  fbc0@@string_ptrMemory                =                                       ( $ )
  fbc0@@string_ptrFile                  =                                       ( $% )
  db ( fbc0@@lenString )                dup                                     ( 0 )

  fbc0@@ctrOpcode                       =                                       ( 0 )
  fbc0@@opcode_ptrMemory                =                                       ( $ )
  fbc0@@opcode_ptrFile                  =                                       ( $% )
  db ( 1 * fbc0@@numOpcode )            dup                                     ( 0 )
  Macro fbc0@@killMySelf
    Purge fbc0@@instruction, fbc0@@operantByte, fbc0@@operantWord, fbc0@@operantDWord, fbc0@@operantQWord, fbc0@@operantString, fbc0@@addString
  End Macro
  Macro fbc0@@instruction               opcode*
    Store                               byte  opcode                            At ( fbc0@@opcode_ptrFile + fbc0@@ctrOpcode )
    fbc0@@ctrOpcode                     =                                       ( fbc0@@ctrOpcode + 1 )
  End Macro
  Macro fbc0@@operantByte               value*
    Store                               byte  value                             At ( fbc0@@opcode_ptrFile + fbc0@@ctrOpcode )
    fbc0@@ctrOpcode                     =                                       ( fbc0@@ctrOpcode + 1 )
  End Macro
  Macro fbc0@@operantWord               value*
    Store                               word  value                             At ( fbc0@@opcode_ptrFile + fbc0@@ctrOpcode )
    fbc0@@ctrOpcode                     =                                       ( fbc0@@ctrOpcode + 2 )
  End Macro
  Macro fbc0@@operantDWord              value*
    Store                               dword value                             At ( fbc0@@opcode_ptrFile + fbc0@@ctrOpcode )
    fbc0@@ctrOpcode                     =                                       ( fbc0@@ctrOpcode + 4 )
  End Macro
  Macro fbc0@@operantQWord              value*
    Store                               qword value                             At ( fbc0@@opcode_ptrFile + fbc0@@ctrOpcode )
    fbc0@@ctrOpcode                     =                                       ( fbc0@@ctrOpcode + 8 )
  End Macro
  Macro fbc0@@operantString             string*
    Local temp
    temp                                fbc0@@addString                         string
    Store                               qword temp                              At ( fbc0@@opcode_ptrFile + fbc0@@ctrOpcode )
    fbc0@@ctrOpcode                     =                                       ( fbc0@@ctrOpcode + 8 )
  End Macro
  Struc fbc0@@addString                 string*
;    displayValue 'address: 0x', fbc0@@offString
    Local size, char, virturtle
    Virtual                             At 0
      virturtle::
      db string
      size                              =                                       ( $ )
    End Virtual
    Store                               word size                               At ( fbc0@@string_ptrFile + fbc0@@offString )
    Repeat ( size )
      Load                              char byte                               from virturtle:( % - 1 )
      Store                             byte char                               At ( fbc0@@string_ptrFile + fbc0@@offString + 2 + % - 1 )
    End Repeat
    .                                   =                                       ( fbc0@@offString + 2 )
    .size                               =                                       ( size )
    fbc0@@offString                     =                                       ( fbc0@@offString + size + 2 )
  End Struc
  import 'fruitbot/assembly'
  Match , language
    display '[asm:fbc0] warn: default language is assembler.', 10
  Else Match =assembler, language
  Else Match =yasic, language
    display '[asm:fbc0] yet another symbolic instruction code!', 10
    import 'yasic/main'
    import 'yasic/operators'
    import 'yasic/optimise'
    import 'yasic/queque'
    import 'yasic/stack'
    import 'yasic/strings'
    import 'yasic/types'
    import 'yasic/variables'
  Else
    err 10, '[asm:fbc0] unknown language »', `language, '«'
  End Match
End Macro
Macro end?.code!
  fbc0@@killMySelf
  Purge fbc0@@killMySelf
  fbc0@@lenString                       =                                       ( fbc0@@offString )
  If ( fbc0@@lenString > 0 )
    uf4@@ctrYapter                      =                                       ( uf4@@ctrYapter + 1 )
    uf4@@addYapter                      fbc0@@yStrings,     fbc0@@lenString,    0,                  0,                  ( fbc0@@string_ptrFile - uf4@@lblYcontent )
  End If
  fbc0@@numOpcode                       =                                       ( fbc0@@ctrOpcode )
  uf4@@ctrYapter                        =                                       ( uf4@@ctrYapter + 1 )
  uf4@@addYapter                        fbc0@@yBytecode,    fbc0@@numOpcode,    0,                  fbc0@@version,      ( fbc0@@opcode_ptrFile - uf4@@lblYcontent )
End Macro
End Match

